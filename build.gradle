buildscript {
    ext {
        lombokVersion = "1.18.6"
        junitVersion = "5.3.2"
        homebankServerVersion = "2.0.0-SNAPSHOT"
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.1.4.RELEASE")
    }
}

plugins {
    id "java"
    id "org.springframework.boot" version "1.5.20.RELEASE"
    id "io.spring.dependency-management" version "1.0.7.RELEASE"

    id "application"

    id 'jacoco'
    id "org.sonarqube" version "2.7.1"

    id 'com.palantir.docker' version "0.22.1"

    id 'idea'
}

apply from: "$rootDir/integration-test.gradle"

jacoco {
    toolVersion = "0.8.4"
}

jacocoTestReport {
    executionData(file("${project.buildDir}/jacoco/test.exec"), file("${project.buildDir}/jacoco/integrationTest.exec"))
    getAdditionalSourceDirs().from(sourceSets.main.allSource.srcDirs)
    getSourceDirectories().from(sourceSets.main.allSource.srcDirs)
    getClassDirectories().from(project.files(sourceSets.main.output))
    reports {
        xml.enabled true
        xml.destination(file("${project.buildDir}/reports/jacoco/all-tests/jacocoAllTestReport.xml"))
        html.enabled true
        html.destination(file("${project.buildDir}/reports/jacoco/all-tests/jacocoAllTestReport"))
        csv.enabled false
    }
}

test {
    useJUnitPlatform()
}

sonarqube {
    properties {
        property "sonar.coverage.jacoco.xmlReportPaths", jacocoTestReport.reports.xml.destination
        properties["sonar.junit.reportPaths"] += integrationTest.reports.junitXml.destination
        properties["sonar.tests"] += sourceSets.integrationTest.allSource.srcDirs
    }
}

idea {
    module {
        testSourceDirs += sourceSets.integrationTest.java.srcDirs
    }
}

version = "${homebankServerVersion}"
sourceCompatibility = 1.9
targetCompatibility = 1.9

mainClassName = "com.cyosp.homebank.server.HomebankServerApplication"

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    compile "org.springframework.cloud:spring-cloud-config-server:2.1.1.RELEASE"
    compile "com.thoughtworks.xstream:xstream:1.4.11.1"
    compile "org.apache.commons:commons-lang3:3.0"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    
    testCompile "org.springframework.boot:spring-boot-starter-test"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Finchley.RELEASE"
    }
}

jar {
    enabled = true
}

docker {
    name "cyosp/homebank-server:${homebankServerVersion}"
    copySpec.from("build/libs").into("build/libs")
}
